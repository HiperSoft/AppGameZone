apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.hipersoft.gamezone"
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 16
        versionName "1.4.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

realm {
    syncEnabled = true
    kotlinExtensionsEnabled = true
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    // Third party
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    ktlint "com.github.shyiko:ktlint:0.14.0"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // GPS Location
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    // Progress wheel
    implementation 'com.pnikosis:materialish-progress:1.7'
    // HTTP networking
    implementation 'com.github.kittinunf.fuel:fuel-android:1.12.0'
    // QR scanner
    implementation 'me.dm7.barcodescanner:zbar:1.9.8'
    // Stepper (material)
    implementation 'com.stepstone.stepper:material-stepper:4.3.1'
    // Currency format EditText
    implementation 'com.github.faranjit:currency-edittext:1.0.1'
    // Manage URLs
    implementation 'com.facebook.fresco:fresco:1.9.0'
    // Rollbar
    implementation 'com.rollbar:rollbar-java:1.4.0'
    implementation 'com.rollbar:rollbar-android:1.4.0@aar'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
